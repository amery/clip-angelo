$FUNCNAME$
  PROW()
$CATEGORY$
  TERMINAL/IO
$SUMMARY$
   Return the current row position of the printhead
$LANG_RU$
   Возвращает текущий номер строки размещения печатающей головки принтера
$SYNTAX$
     PROW() --> nRow
$LANG_RU$
     PROW() --> номер строки
$RETURNS$
     PROW() returns an integer numeric value that represents the number of
     the current line sent to the printer.  The beginning row position is
     zero.
$LANG_RU$
     PROW() возвращает номер текущей строки, выдаваемой на принтер, в виде
     целого числа. Нумерация строк начинается с нуля.
$DESCRIPTION$
     PROW() is a printer function that reports the row position of the
     printhead after the last print operation.  PROW() is updated only if
     either SET DEVICE TO PRINTER or SET PRINTER ON is in effect.  PROW() is
     like ROW() except that it relates to the printer rather than the screen.
     PROW() is updated in the following ways:

       Application startup sets PROW() to zero

       EJECT resets PROW() to zero

       A print operation sets PROW() to the last row print position

       SETPRC() sets PROW() to the specified row position

     PROW() used with PCOL() prints a value to a new row relative to the last
     row printed.  If the printhead is positioned to a new row with a control
     code, a line feed (CHR(10)), or form feed (CHR(12)), PROW() is not
     updated and, therefore, will not return the expected value.  To prevent
     this discrepancy, reset PROW() to the correct value with SETPRC() after
     sending any of these characters to the printer.
$LANG_RU$
     PROW() - это функция принтера, которая сообщает номер строки, где
     находится печатающая головка после последней операции печати.

     Функция PROW() может обновлять свои значения только при выполненных
     установках SET DEVICE TO PRINTER или SET PRINTER ON. PROW()
     аналогична функции ROW(), за исключением того, что она относится к
     принтеру, а не к экрану. Значения PROW() могут быть изменены в
     следующих случаях:

      В начале программы значение PROW() - ноль

      EJECT сбрасывает PROW() в ноль

      Операция печати устанавливает PROW() на позицию последней
       отпечатанной строки

      SETPRC() устанавливает PROW() на заданную позицию строки

     PROW() используется в сочетании с функцией PCOL() для выбора новой
     строки для печати относительно последней отпечатанной строки.
     Если печатающая головка устанавливается на новую строку с помощью
     управляющего кода перевод строки (CHR(10)) или прогон страницы
     (CHR(12)), то значение функции PROW() не обновляется и, таким
     образом, не возвращает ожидаемого значения.

     Чтобы предотвратить это противоречие, PROW() устанавливается на
     правильное значение с помощью SETPRC() после посылки на принтер
     какого-либо из этих символов.
$EXAMPLES$
       This example uses PROW() and SETPRC() to create a simple one-
	across label program that prints with @...SAY instead of ?:

	USE Customer INDEX CustName NEW
	SET DEVICE TO PRINTER
	SETPRC(2, 0)
	DO WHILE !EOF()
	   @ PROW(), 3 SAY CustName
	   @ PROW() + 1, 3 SAY RTRIM(City) + ",;
		  " + RTRIM(State) + ZipCode
	   @ PROW() + 1, 3 SAY Phone PICTURE "@R ;
		 (999) 999-9999"
	   SETPRC(2, 0)
	   SKIP
	ENDDO
	SET DEVICE TO SCREEN
	CLOSE
$LANG_RU$
      Этот пример показывает, как использовать PROW() и SETPRC() для
       создания программы, которая для вывода на печать использует вместо
       команды ? команду @...SAY:

       USE Customer INDEX CustName NEW
       SET DEVICE TO PRINTER
       SETPRC(2, O)
       DO WHILE !EOF()
	  @ PROW(), 3 SAY CustName
	  @ PROW()+1,3 SAY RTRIM(City) +","+ RTRIM(State)+ZipCode
	  @ PROW()+1,3 SAY Phone PICTURE "@R (999) 999-9999"
	  SETPRC(2,O)
	  SKIP
       ENDDO
       SET DEVICE TO SCREEN
       CLOSE
$SEEALSO$
  PCOL(),QOUT(),ROW(),COL(),SETPRC()
$END$
